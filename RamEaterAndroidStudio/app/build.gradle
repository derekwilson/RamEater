apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "derekwilson.net.rameater"
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 9
        versionName "1.0.5"
    }

    signingConfigs {
        release {
            storeFile file("../../AndroidSupport/deploy/derek.keystore")
            storePassword System.getenv("RAMEATER_RELEASE_PASSWORD")
            keyAlias "rameater"
            keyPassword System.getenv("RAMEATER_RELEASE_PASSWORD")
        }
    }

    // uncover to debug any signing issues
    afterEvaluate{
        //println "After evel Store password ==>" + android.signingConfigs.release.storePassword
        //println "After eval Key password ==>" + android.signingConfigs.release.keyPassword
    }

    // Customise the output apk filename to be "recommender-buildtype-version.apk"
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "rameater-${variant.name}-${variant.versionName}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'NOTICE'
        exclude 'APK LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // android support libs
    implementation 'com.android.support:support-v4:26.0.2'
    implementation 'com.android.support:appcompat-v7:26.0.2'
    implementation 'com.android.support:design:26.0.2'
}
